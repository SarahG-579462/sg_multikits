//1. TP2 FILE//
BACKUP ~sg_multikits/backup~ 
AUTHOR ~SadieGamer (SG),  sg2475962@gmail.com~ 
VERSION ~v0.14~
ALWAYS
  COPY ~%MOD_FOLDER%/data~ ~override~
  INCLUDE "%MOD_FOLDER%/lib/sg#install_multikit.tpa"
  
  OUTER_SET DEBUG = 2
  LAM GET_2DAS
  ACTION_IF IS_AN_INT "%_N_PROMPT_TOTAL%" BEGIN
    LAF rglobal STR_VAR name = "_N_PROMPT_TOTAL" RET _N_PROMPT_TOTAL = value END
  END ELSE BEGIN 
    
    LAF wglobal INT_VAR value = 0 - 1 STR_VAR name = "_N_PROMPT_TOTAL" END
    OUTER_SET _N_PROMPT_TOTAL = 0 - 1
  END
  
END
//1.2. LANGUAGES//

AUTO_TRA ~%MOD_FOLDER%/translations/%s~
LANGUAGE ~English~ ~english~ ~%MOD_FOLDER%/translations/english/setup.tra~

//---------------------//
//PROMPT AND BUILD KITS//
/* //---------------------//
BEGIN "TEST COMPONENT" DESIGNATED 1
  LAF prompt_yesno STR_VAR prompt = "testing [y/n]" RET output END
  LAF rglobal STR_VAR name = "_N_PROMPT_TOTAL" RET _N_PROMPT_TOTAL = value END PRINT "_N_PROMPT_TOTAL: %_N_PROMPT_TOTAL%"
  LAF prompt_yesno STR_VAR prompt = "testing [y/n]" RET output END
  LAF rglobal STR_VAR name = "_N_PROMPT_TOTAL" RET _N_PROMPT_TOTAL = value END PRINT "_N_PROMPT_TOTAL: %_N_PROMPT_TOTAL%"
  LAF prompt_yesno STR_VAR prompt = "testing [y/n]" RET output END
  LAF rglobal STR_VAR name = "_N_PROMPT_TOTAL" RET _N_PROMPT_TOTAL = value END PRINT "_N_PROMPT_TOTAL: %_N_PROMPT_TOTAL%"
 */
BEGIN @100 DESIGNATED 100 // Build (allows to uninstall no_log_record)
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @10001
// this component standardizes backstabs. It also is used so we can uninstall multikits.
OUTER_SPRINT info @101
PRINT "%info%"
INCLUDE "%MOD_FOLDER%/lib/sg#backstab.tpa"

LAF backup_2da END
LAF REINITIALIZE_UNIQUE_KITNAMES END
LAF clabify_backstab END

BEGIN @200 
DESIGNATED 200 // PSEUDO-DRUID
DEPRECATED ~Sorry, this component isn't ready yet.~
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @10001
REQUIRE_COMPONENT  ~setup-sg_multikits.tp2~ ~100~ ~Must have installed base component first.~
INCLUDE "%MOD_FOLDER%/lib/sg#pseudodruid.tpa"
OUTER_SPRINT info @201
PRINT "%info%" 

BEGIN @1000 
//NO_LOG_RECORD 
DESIGNATED 1000 // Build 
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @10001
REQUIRE_COMPONENT  ~setup-sg_multikits.tp2~ ~100~ ~Must have installed base component first.~

// This component allows you to create multikits; it can only be uninstalled by UNINSTALLING component 100, but can be installed multiple times.
OUTER_SPRINT info @1002 
PRINT "%info%"

OUTER_SET createkit = 1 
OUTER_SET _get2da = 0
OUTER_SPRINT nother ""
OUTER_WHILE (createkit > 0) BEGIN
  // The multikit creator function:
  LAF prompt_yesno STR_VAR prompt = EVAL "Create a%nother% kit? [Y/N]" RET createkit = output END
  ACTION_IF createkit BEGIN
    LAF multikit INT_VAR _get2da = _get2da END
  END
  OUTER_SET _get2da = 1
  OUTER_SPRINT nother "nother"
END 
//LAF backup_2da END

OUTER_SPRINT backupdir_100 ~%MOD_FOLDER%/backup/100~
OUTER_SPRINT backupdir_1000 ~%MOD_FOLDER%/backup/1000~
OUTER_SET INSTALL_COUNT = 1
OUTER_SPRINT newdir ~%backupdir_1000%/%INSTALL_COUNT%~

OUTER_WHILE (DIRECTORY_EXISTS ~%newdir%~) BEGIN
  OUTER_SET INSTALL_COUNT += 1
  OUTER_SPRINT newdir ~%backupdir_1000%/%INSTALL_COUNT%~
END
ACTION_IF ! (DIRECTORY_EXISTS ~%newdir%~) BEGIN
  MKDIR ~%newdir%~
END
COPY + ~%backupdir_1000%~ ~%newdir%~
OUTER_SPRINT backupfile_100 ~\([A-Z]*\.100\(\.[A-Z]\)?\)~
OUTER_SPRINT backupfile_1000 ~\([A-Z]*\.100\(\.[A-Z]\)?\)~
OUTER_SPRINT mappings_1000 ~%newdir%/MAPPINGS.1000~
ACTION_IF (FILE_EXISTS ~%mappings_1000%~) BEGIN
  PRINT "RE-ALIGNING %mappings_1000%..."
  COPY + ~%mappings_1000%~ ~%newdir%~
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~TB#"SPACE"%backupdir_1000%~ ~TB#"SPACE"%newdir%~
  
END ELSE BEGIN FAIL "COULD NOT FIND %mappings_1000%..." END
COPY + ~%backupdir_100%~ ~%backupdir_100%~
  
  PATCH_IF NOT (~%SOURCE_FILE%~ STRING_MATCHES_REGEXP ~%backupfile_100%~) BEGIN
    INNER_PATCH_SAVE equiv_1000 ~%SOURCE_FILE%~ BEGIN
      REPLACE_TEXTUALLY EXACT_MATCH ".100" ".1000"
    END
    PATCH_PRINT "SOURCE_FILE %SOURCE_FILE% IS A BACKUP FILE... APPENDING WITH %newdir%/%equiv_1000%"
    PATCH_IF (FILE_EXISTS ~%newdir%/%equiv_1000%~) BEGIN
      INSERT_FILE 0x0 ~%newdir%/%equiv_1000%~
    END
  END
  
  
BEGIN @2000 
DESIGNATED 2000 // Dynamic Kit Creation
DEPRECATED ~Sorry, this component isn't ready yet.~
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee iwdee eet~ @10001
REQUIRE_COMPONENT  ~setup-sg_multikits.tp2~ ~100~ ~Must have installed base component first.~
INCLUDE "%MOD_FOLDER%/lib/sg#dynamic.tpa"
OUTER_SPRINT info @2001
PRINT "%info%"
